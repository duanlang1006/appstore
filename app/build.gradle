apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.shly.appstore"
        minSdkVersion 10
        targetSdkVersion 21
        versionCode 45
        versionName "2.0.045"
        multiDexEnabled true
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "leadexceed_buildin"]
    }

    // 移除lint检查的error
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file("../dsc.keystore")
            storePassword "shly123456"
            keyAlias "dbservice"
            keyPassword "shly123456"
        }

        def config = release {
            storeFile file("../dsc.keystore")
            storePassword "shly123456"
            keyAlias "dbservice"
            keyPassword "shly123456"
        }
        config
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled false
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为appstore_v1.0_wandoujia.apk
                        def fileName = "appstore_v${defaultConfig.versionName}_${variant.productFlavors[0].name}_${variant.buildType.name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        leadexceed_buildin {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    task apkZip(type: Zip) {
        classifier = 'build'
        from 'build/outputs/apk/'
        include 'appstore_*.apk'
        from "../"
        include 'version_readme.txt'
        destinationDir file('.')
        archiveName "../appstore_v${defaultConfig.versionName}.zip"
    }

    task setTag(type: Exec) {
        executable "git"
        args "tag"
        args "-af"
        args "appstore_v${defaultConfig.versionName}"
        args "-m"
        args "release for appstore_v${defaultConfig.versionName}"
    }

    task pushTag(type: Exec) {
        executable "git"
        args "push"
        args "origin"
        args "--tags"
        args "-f"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.code.gson:gson:2.2.4'
    compile project(':extras:impl')
    compile project(':extras:applitecommon')
    compile project(':extras:mitUpdateSDK')
    compile project(':bundlers:applitelogo')
    compile project(':bundlers:applitehomepage')
    compile project(':bundlers:applitedetail')
    compile project(':bundlers:applitedm')
    compile project(':bundlers:applitesearch')
    compile project(':bundlers:appliteupdate')
    compile 'com.jiechic.library:xUtils:2.6.14'
}
